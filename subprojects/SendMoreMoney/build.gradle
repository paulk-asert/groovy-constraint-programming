/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = 'SendMoreMoneyChocoJava'

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation "org.choco-solver:choco-solver:$chocoVersion"
    implementation "com.google.ortools:ortools-java:$ortoolsVersion"
    implementation("org.jacop:jacop:$jacopVersion") {
        exclude(group: 'org.slf4j', module :'slf4j-log4j12')
    }
    implementation('com.javasolver:jsr331-choco:2.2.0') {
        exclude(group: 'com.javasolver', module :'jsr331-choco2')
    }
    runtimeOnly files('lib/choco-solver-2.1.5.jar')
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.12'
}

def runall = tasks.register('runAll') {
    group 'Application'
    dependsOn run
}

FileUtil.baseNames(sourceSets.main.allSource.files).each { name ->
    def subtask = tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
    runall.configure {
        dependsOn subtask
    }
}
