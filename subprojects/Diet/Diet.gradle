/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'
def os = System.getProperty("os.name").toLowerCase()

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation "org.choco-solver:choco-solver:$chocoVersion"
    implementation 'org.ojalgo:ojalgo:48.4.2'
    implementation 'org.jacop:jacop:4.8.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.optaplanner:optaplanner-core:7.46.0.Final'
    implementation 'ch.qos.logback:logback-classic:1.2.3' // optional for optaplanner debugging
    // better publication of platform specific jars is expected from Google soon
    // until then, use different versions based on ad-hoc publication from 3rd parties
    if (os.contains('linux')) {
        implementation 'com.panavis.open-source:google-ortools-java:7.8.7961'
        implementation 'com.panavis.open-source:ortools-linux-x86-64:7.8.7959'
    } else if (os.contains('windows')) {
        // jars from ortools install are copied to lib directory
        implementation files('lib/ortools-java-8.0.8283.jar')
        implementation files('lib/ortools-win32-x86-64-8.0.8283.jar')
        implementation 'com.panavis.open-source:google-ortools-java:7.8.7961' // get other dependencies
    } else if (os.contains('macosx')) {
        implementation 'io.github.ejif.ortools:ortools:0.0.2'
        implementation 'io.github.ejif.ortools:ortools-macosx:0.0.2'
    }
}

FileUtil.baseNames(file("$projectDir/src/main/groovy")).each { name ->
    task "run$name"(type: JavaExec) {
        dependsOn compileGroovy
        group 'Script'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        main = name
    }
}
