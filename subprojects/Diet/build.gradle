/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

ext.ojalgo = JavaVersion.current().isJava8() ? ojalgoLegacyVersion : ojalgoVersion

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation "org.choco-solver:choco-solver:$chocoVersion"
    implementation "org.ojalgo:ojalgo:$ojalgo"
    implementation "org.jacop:jacop:$jacopVersion"
    implementation 'org.apache.commons:commons-math4-legacy:4.0-beta1'
    implementation 'org.hipparchus:hipparchus-optim:3.1'
    implementation "com.google.ortools:ortools-java:$ortoolsVersion"
}

def runall = tasks.register('runAll') {
    group 'Application'
}
FileUtil.baseNames(sourceSets.main.allSource.files, ['DietChocoReal']).each { name ->
    def subtask = tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
    runall.configure {
        dependsOn subtask
    }
}
